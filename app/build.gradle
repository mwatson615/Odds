apply plugin: 'com.android.application'
apply plugin: 'easymvp'
apply plugin: 'jacoco'

jacoco {
    toolVersion '0.8.1'
}

def keystorePropsFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropsFile))

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.rosebay.odds"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 2
        versionName "1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField('String', 'ImgApiKey', ProdApiKey)
            buildConfigField('String', 'AdUnitID', AdUnitID)
            buildConfigField('String', 'AdMobID', AdMobID)
            signingConfig signingConfigs.config
        }
        debug {
            testCoverageEnabled true
            buildConfigField('String', 'ImgApiKey', DevApiKey)
        }
        buildTypes.each {
            it.buildConfigField('String', 'BaseImageUrl', BaseImageUrl)
            it.buildConfigField('String', 'CustomSearchEngine', CustomSearchEngine)
            it.buildConfigField('String', 'BaseFirebaseUrl', BaseFirebaseUrl)
            it.buildConfigField('String', 'BaseGCFUrl', BaseGCFUrl)
        }
    }
    testOptions {
        execution 'ANDROID_TEST_ORCHESTRATOR'
        animationsDisabled true
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:customtabs:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.google.android.gms:play-services-analytics:15.0.2'
    implementation 'com.google.android.gms:play-services-ads:15.0.1'
    implementation 'com.google.apis:google-api-services-customsearch:v1-rev59-1.23.0'
    implementation 'com.google.firebase:firebase-core:15.0.2'
    implementation 'com.google.firebase:firebase-database:15.0.1'
    implementation 'com.google.firebase:firebase-ads:15.0.1'
    implementation 'com.github.frangsierra:rx2firebase:1.1.3'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    implementation 'android.arch.lifecycle:common-java8:1.1.1'
    annotationProcessor 'android.arch.lifecycle:compiler:1.1.1'
    implementation 'android.arch.persistence.room:runtime:1.1.0'
    annotationProcessor 'android.arch.persistence.room:compiler:1.1.0'
    implementation 'android.arch.persistence.room:rxjava2:1.1.0'
    implementation 'com.jakewharton:butterknife:8.8.1'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.5'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.google.dagger:dagger:2.15'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.15'
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.1.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    implementation 'com.jakewharton.timber:timber:4.6.1'
    implementation 'com.android.support:multidex:1.0.3'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'recyclerview'
    })
    androidTestImplementation('com.android.support.test.espresso:espresso-contrib:3.0.2') {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'org.mockito:mockito-android:2.7.22'
    androidTestImplementation 'android.arch.core:core-testing:1.1.1'
    androidTestUtil 'com.android.support.test:orchestrator:1.0.1'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.7.22'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
}

def coverageSourceDirs = ['../app/src/main/java']

def classDirs = fileTree(dir: '../app/build/intermediates/classes/debug/com',
        excludes: [
                '**/R.class',
                '**/R$*.class',
                '**/BuildConfig.*',
                '**/Manifest*.*',
                '**/*ViewBinding*.*',
                '**/*_MembersInjector.class',
                '**/*_MembersInjector*.*',
                '../*Component*.*',
                '**/*_Provide*Factory*.*',
                '**/*_Factory.*',
                '**/Manifest*.*',
                '**/*$Lambda$*.*',
                '**/*Dagger*.*',
                '**/*Module.*',
                '**/*Module*.*',
                '**/*_ViewDelegate*.*',
                '**/*RoomModule*.class',
                '../*ContextModule*.class'])

task jacocoCombinedDebugCoverageReport(type: JacocoReport, dependsOn: ['connectedDebugAndroidTest', 'testDebugUnitTest']) {
    group = "Reporting"
    description = "Combined Jacoco coverage report for debug build"
    classDirectories = classDirs
    additionalSourceDirs = files(coverageSourceDirs)
    sourceDirectories = files(coverageSourceDirs)
    executionData = files([fileTree(dir: 'build/outputs/code-coverage/connected', include: '**/*.ec'),
                           fileTree(dir: 'build/jacoco', include: '**/*.exec')])
    onlyIf = {
        true
    }
    reports {
        xml.enabled true
        html.enabled true
    }
}

repositories {
    maven {
        url 'https://maven.google.com'
        // Alternative URL is 'https://dl.google.com/dl/android/maven2/'
    }
}

apply plugin: 'com.google.gms.google-services'